import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'com.github.johnrengelman.shadow'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.26'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"


    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}


modrinth {
    token = System.getenv("MODRINTH_MOD_PUBLISH")
    projectId = "saplanting"
    versionName = archives_name + "-neoforge-" + mod_version
    versionNumber = mod_version
    versionType = mod_version.contains("pre") ? "beta" : "release"
    uploadFile = remapJar
    gameVersions = [minecraft_version]
    loaders = ["neoforge"]
    changelog = rootProject.file("changelog.md").text
    dependencies {
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_MOD_PUBLISH")
    def mainFile = upload('576864', remapJar)
    mainFile.displayName = archives_name + "-neoforge-" + mod_version
    mainFile.releaseType = mod_version.contains("pre") ? "beta" : "release"
    mainFile.changelog = rootProject.file("changelog.md").text
    mainFile.changelogType = 'markdown'
    mainFile.gameVersions = [minecraft_version]
    mainFile.addJavaVersion("Java 21")
    mainFile.addEnvironment("server", "client")
    mainFile.addModLoader("neoforge")
    mainFile.addRequirement("architectury-api")
    mainFile.addIncompatibility("croparia")
}